plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '2.0.3'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation ('org.apache.poi:poi-ooxml:3.17'){
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.xmlbeans', module: 'xmlbeans'

        //these are only needed when handling Visio files, remove it if you would like to use Visio support
        exclude group:'com.github.virtuald', module: 'curvesapi'
        exclude group:'commons-codec', module: 'commons-codec'
        exclude group:'org.apache.commons', module: 'commons-collections4'
    }
    implementation ('org.apache.poi:poi:3.17'){
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.xmlbeans', module: 'xmlbeans'

        //these are only needed when handling Visio files, remove it if you would like to use Visio support
        exclude group:'com.github.virtuald', module: 'curvesapi'
        exclude group:'commons-codec', module: 'commons-codec'
        exclude group:'org.apache.commons', module: 'commons-collections4'
    }
    implementation 'com.fasterxml:aalto-xml:1.1.0'
    //don't use 2.6.0, it cause :  Program type already present: org.apache.xmlbeans.xml.stream.Location
    implementation 'org.apache.xmlbeans:xmlbeans:3.0.0'

}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

shadowJar {
    exclude 'repackage/**'

    exclude 'LICENSE.txt'
    exclude 'NOTICE.txt'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/maven/**'

    // these are only needed when handling Visio files, remove it if you would like to use Visio support
    dependencies {
        exclude(dependency('com.github.virtuald:curvesapi'))
        exclude(dependency('commons-codec:commons-codec'))
        exclude(dependency('org.apache.commons:commons-collections4'))
    }

    // Relocate javax dependencies so Android does not choke
    relocate 'javax.xml.namespace', 'org.apache.poi.javax.xml.namespace'
    relocate 'javax.xml.stream', 'org.apache.poi.javax.xml.stream'
    relocate 'javax.xml.XMLConstants', 'org.apache.poi.javax.xml.XMLConstants'
    // java.awt is not available, but class Color is used in APIs in POI, therefore
    // relocate this class to another one where we can include a rewrite
    relocate 'java.awt.Color', 'org.apache.poi.java.awt.Color'
}

jar.dependsOn shadowJar
test.dependsOn shadowJar


